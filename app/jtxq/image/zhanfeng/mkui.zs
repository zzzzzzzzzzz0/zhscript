#!../../bin/gtkmmsh lib2/window1.glade --。
隧道窗口宽高、300、600。
隧道窗口居中。
隧道内容、text、下原样这是 zhanfeng 样式 ui 生成程序。
提供有：
随机方案1——根据棋盘宽高重新生成棋子位置，
	随机起名，竖铺，无序号
随机方案2——根据棋盘宽高重新生成棋子位置，
	随机起名，序号天干
几种样式按钮，点击设置，
然后点击“看下”便能启动程序来看效果上原样。

隧道标题为zhanfeng样式ui生成。

加载lib/file4。
加载lib/defi。
加载lib/regexpr4。
加载lib/gjk4。
加载lib/gjke4。

新建数组姓【主】、1、
赵、钱、孙、李、周、吴、郑、王、冯、陈、褚、卫、蒋、沈、韩、杨、
朱、秦、尤、许、何、吕、施、张、孔、曹、严、华、金、魏、陶、姜、
戚、谢、邹、喻、柏、水、窦、章、云、苏、潘、葛、奚、范、彭、郎、
澹台、公冶、宗政、濮阳、淳于、单于、太叔、申屠、
公孙、仲孙、轩辕、令狐、钟离、宇文、长孙、慕容、
鲜于、闾丘、司徒、司空、亓官、司寇、仉督、子车、
颛孙、端木、巫马、公西、漆雕、乐正、壤驷、公良、
拓跋、夹谷、宰父、穀梁、晋楚、闫法、汝鄢、涂钦、
段干、百里、东郭、南门、呼延、归海、羊舌、微生、
岳帅、缑亢、况後、有琴、梁丘、左丘、东门、西门、
商牟、佘佴、伯赏、南宫、墨哈、谯笪、年爱、阳佟、
。
新建数组名【主】、1、
凤娇、静婉、碧娴、绛妩、嬿妮、小娈、嫣然、静婵、玉姣、月好、芳娉、淑婷、芳姿、琼娥、妙常、玉嫩、
浣花、春草、湘兰、沅蕙、荃君、芷君、纕英、舜华、琼芝、瘦菊、莲佩、碧荷、漱菡、香梅、杨柳、杏君、
鸣凤、玉鸾、春莺、飞燕、秋雁、鸳鸯、
彩纨、曼艳、黛英、多丽、碧倩、小红、绛珠、红荷、绿英、紫绡、素纷、赤缨、玉青、黄绮、翠菡、
念奴、莫愁、痴玉、媚娘、如梦、佩柔、柔嘉、
贞秀、淑仪、玉端、丽庄、娴倩、静玉、
。
调用‘随机数种’、-1。

赋予目录【主】以调用‘得目录名’、先调用‘最终文件名’、‘参数0’了。

赋予ui【主】以‘目录’ui.glade。
赋予ui-【主】以‘目录’ui-.glade。
显示‘ui’↙‘ui-’↙。

加载lib/xml4。
赋予xml【主】以调用‘xml_from_file’、‘ui-’。

定义“遍历”以下代码
	别名节点以参数1。
	赋予第1个以1。
	循环先
		赋予名以调用‘xml_node_name’、‘节点’。
		如果‘第1个’并且‘名’等于text那么赋予名以。
		当‘名’先
			等于comment那么跳出。
			‘名’那么先
				赋予属性以调用‘xml_attr’、‘节点’。
				循环先
					如果不‘属性’那么跳出。
					赋予属名以调用‘xml_attr_name’、‘属性’。
					赋予属值以调用‘xml_attr_text’、‘属性’。
					
					当‘属名’先
						等于class或者等于id那么先
							赋予‘属名’【主】以‘属值’。
							跳出。
						了。
						等于name那么先
							当‘属值’先
								等于width_request或者等于height_request那么先
									如果‘class【主】’等于GtkFixed并且‘id【主】’等于di那么
										赋予捕【主】以‘属值’。
									跳出。
								了。
							了。
							跳出。
						了。
					了。
					
					赋予属性以调用‘xml_next_attr’、‘属性’。
				了。
			了。
		了。
		
		如果不‘名’并且存在捕【主】那么先
			赋予‘捕【主】’【主】以调用‘xml_node_text’、‘节点’。
			删除捕【主】。
		了。
	
		赋予子以调用‘xml_children’、‘节点’。
		如果‘子’那么遍历‘子’。

		赋予节点以调用‘xml_next’、‘节点’。
		如果‘节点’否则跳出。
		赋予第1个以0。
	了。
上代码。
遍历先调用‘xml_root’、‘xml’了。
显示‘width_request’x‘height_request’↙。

赋予棋子间【主】以10。
赋予棋子高【主】以算术(‘height_request’-9*‘棋子间’)/9、0。
（‘棋子高’/270*194）
赋予棋子宽【主】以算术(‘width_request’-10*‘棋子间’)/10、0。
显示‘棋子宽’x‘棋子高’↙。

定义“显示一【主】”以下代码
	（显示‘参数1’。）
	赋予出【主】以‘出【主】’‘参数1’。
上代码。

定义“显示二【主】”以下代码
	隧道插入、text、‘参数1’↙。
	隧道不堵塞。
上代码。

加载‘目录’../../zs/defi.zs。
定义遇到棋子坐标【主】以下代码
	别名标以参数1。
	显示‘id【主】’.‘标’。
	如果调用‘串位置方法’、‘id【主】’、pao大于0那么先
		当‘id【主】’先
			等于heipao1那么先
				赋予1以1。
				赋予2以2。
			了。
			等于heipao2那么先
				赋予1以7。
				赋予2以2。
			了。
			等于hongpao1那么先
				赋予1以1。
				赋予2以7。
			了。
			等于hongpao2那么先
				赋予1以7。
				赋予2以7。
			了。
		了。
		当‘标’先
			等于x那么先
				赋予换【主】以算术(‘棋子宽’+‘棋子间’)*‘2’。
				跳出。
			了。
			等于y那么先
				赋予换【主】以算术‘1’*(‘棋子高’+‘棋子间’)。
				跳出。
			了。
		了。
		显示=‘换【主】’↙。
		退出。
	了。
	赋予号以搜索数组棋子【主】、‘id【主】’。
	显示(号‘号’)。
	赋予bing以如果调用‘串位置方法’、‘id【主】’、bing大于0那么1否则0。
	如果调用‘串位置方法’、‘id【主】’、zu大于0或者‘bing’那么先
		当‘标’先
			等于x那么先
				赋予换【主】以算术(‘棋子宽’+‘棋子间’)*如果‘bing’那么6否则3。
				跳出。
			了。
			等于y那么先
				如果‘bing’那么
					赋予号以算术‘号’-5。
				赋予换【主】以算术(‘号’-12)*2*(‘棋子高’+‘棋子间’)。
				跳出。
			了。
		了。
		显示=‘换【主】’↙。
		退出。
	了。
	当‘号’先
		小于等于9或者大于等于24那么先
			当‘标’先
				等于x那么先
					如果‘号’小于等于9那么
						赋予换【主】以0
					否则
						赋予换【主】以算术(‘棋子宽’+‘棋子间’)*9。
					跳出。
				了。
				等于y那么先
					如果‘号’大于等于24那么
						赋予号以算术‘号’-23。
					赋予换【主】以算术(‘号’-1)*(‘棋子高’+‘棋子间’)。
					跳出。
				了。
			了。
			跳出。
		了。
		那么赋予换【主】以400。
	了。
	显示=‘换【主】’↙。
上代码。

定义“遍历【主】”以下代码
	别名节点以参数1。
	赋予第1个以1。
	循环先
		赋予名以调用‘xml_node_name’、‘节点’。
		如果‘第1个’并且‘名’等于text那么赋予名以。
		当‘名’先
			等于comment那么显示一<!--，跳出。
			‘名’那么先
				显示一<‘名’。
	
				赋予属性以调用‘xml_attr’、‘节点’。
				循环先
					如果不‘属性’那么跳出。
					赋予属名以调用‘xml_attr_name’、‘属性’。
					赋予属值以调用‘xml_attr_text’、‘属性’。
					
					当‘属名’先
						等于class或者等于id那么先
							赋予‘属名’【主】以‘属值’。
							跳出。
						了。
						等于name那么先
							当‘属值’先
								等于width_request那么先
									如果‘class【主】’等于GtkImage那么赋予换【主】以‘棋子宽’。
									跳出。
								了。
								等于height_request那么先
									如果‘class【主】’等于GtkImage那么赋予换【主】以‘棋子高’。
									跳出。
								了。
								等于x或者等于y那么先
									（显示‘class【主】’→‘id【主】’→‘属值’↙。）
									如果‘class【主】’等于GtkButton那么遇到棋子坐标‘属值’。
									跳出。
								了。
								等于tooltip_text那么先
									如果‘class【主】’等于GtkButton那么先
										当‘方案’先
											等于随机方案1或者等于随机方案2那么先
												赋予姓号以调用‘随机数方法’、‘姓数目’、1。显示(姓‘姓号’)。
												赋予名号以调用‘随机数方法’、‘名数目’、1。显示(名‘名号’)。
												跳出。
											了。
										了。
										当‘方案’先
											等于随机方案1那么先
												赋予换【主】以去拼音‘id【主】’、
													‘姓之‘姓号’’‘名之‘名号’’↙属↙└、↙兵↙种↙└、-。
												跳出。
											了。
											等于随机方案2那么先
												赋予换【主】以去拼音‘id【主】’、
													‘姓之‘姓号’’‘名之‘名号’’↙、↙、、干。
												跳出。
											了。
										了。
										显示二‘id’:。
										显示二‘换’。
									了。
									跳出。
								了。
							了。
							跳出。
						了。
					了。
					
					显示一“ ‘属名’="‘属值’"”。
					赋予属性以调用‘xml_next_attr’、‘属性’。
				了。
				显示一>。
			了。
		了。

		如果不‘名’并且存在换【主】那么先
			显示一‘换【主】’。
			删除换【主】。
		了否则
			显示一先调用‘xml_node_text’、‘节点’了。
	
		赋予子以调用‘xml_children’、‘节点’。
		如果‘子’那么遍历‘子’。

		当‘名’先
			等于comment那么显示一-->，跳出。
			‘名’那么显示一</‘名’>字符10。
		了。
		赋予节点以调用‘xml_next’、‘节点’。
		如果‘节点’否则跳出。
		赋予第1个以0。
	了。
上代码。

定义“执行选定方案”【主】以下代码
	隧道内容、text、。
	赋予出【主】以。
	显示一“<?xml version="1.0" encoding="UTF-8" standalone="no"?>”。
	遍历先调用‘xml_root’、‘xml’了。
	显示字符10。

	加载lib/doscmd4。
	调用‘echo’、‘出【主】’、‘ui’。
上代码。

隧道可见、exec2、1。
隧道提示文字、exec、随机方案1。
隧道提示文字、exec2、随机方案2。
隧道侦听、点击、exec、exec2、下代码
	赋予方案【主】以‘参数1’。
	执行选定方案。
上代码。

隧道可见、exec3、1。
隧道提示文字、exec3、看下效果。
隧道侦听、点击、exec3、下代码
	加载lib/stdc。
	调用‘改变目录函数’、‘目录’../..。
	执行./start.sh&。
上代码。

（调用‘xml_free’、‘xml’。）
定义运行命令【主】以下代码
	别名命令以参数1。
	如果调用‘头匹配方法’、‘命令’、“ ”否则
		赋予命令以先Ctrl+U了先Ctrl+K了‘命令’。
	如果调用‘尾匹配方法’、‘命令’、“ ”否则
		赋予命令以‘命令’换行。
	使主窗、输入、‘命令’。

	别名命令以参数1。
	如果调用‘头匹配方法’、‘命令’、“cd ”
	或者调用‘头匹配方法’、‘命令’、“ls ”
	或者调用‘头匹配方法’、‘命令’、“ ”那么退出。
	如果‘命令’等于‘上次命令’或者等于‘上上次命令’或者等于cd那么退出。
	赋予上上次命令【主】以‘上次命令’。
	赋予上次命令【主】以‘命令’。
	如果不存在上次命令钮号【主】那么先
		赋予代码以下代码
			使主窗
		上代码先如果存在-iv【主】那么下代码
			、图标夹、遍历
		上代码否则下代码
			#a、遍历、盒
		上代码了下代码
			、‘参数栈’
		上代码。
		解释‘代码’、下代码
			别名名以参数1。
			别名号以参数‘参数数目’。
			当‘名’先
				等于上次命令那么先
					赋予上次命令钮号【主】以‘号’。
					跳出。
				了。
				等于上上次命令那么先
					赋予上上次命令钮号【主】以‘号’。
					跳出。
				了。
			了。
		上代码。
		显示上次命令钮号‘上次命令钮号’换行上上次命令钮号‘上上次命令钮号’换行。
	了。
	如果存在-iv【主】那么先
		使主窗、图标夹、设、‘上次命令钮号’、“提示”、‘命令’。
		如果‘上上次命令’那么
			使主窗、图标夹、设、‘上上次命令钮号’、“提示”、‘上上次命令’。
	了否则先
		使主窗#a、工具提示、‘上次命令钮号’、‘命令’。
		如果‘上上次命令’那么
			使主窗#a、工具提示、‘上上次命令钮号’、‘上上次命令’。
	了。
上代码。

赋予上次命令【主】以
赋予上上次命令【主】以。
定义这上次命令【主】以下代码
	运行命令‘上次命令’。
上代码。
定义这上上次命令【主】以下代码
	运行命令‘上上次命令’。
上代码。

定义刷新命令菜单【主】以下代码
	新建菜单--命令。
	加载‘命令菜单文件’。
	调用‘增至菜单’、‘命令菜单’、
	-、
	代码为“文字编辑器打开“‘命令菜单文件’””、图标为gtk-edit、为编辑、
	代码为“刷新命令菜单”、图标为gtk-refresh、为刷新。
上代码。

赋予命令菜单文件【主】以‘配置目录’/cmd-menu.zs。
如果调用‘exist’、‘命令菜单文件’否则
	调用‘echo’、先调用‘type’、‘应用目录’/cmd-menu.zs了、‘命令菜单文件’。
刷新命令菜单。

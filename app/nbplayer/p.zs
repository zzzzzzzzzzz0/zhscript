#!/usr/lib/zhscript/cairogsh --。
加载lib/defi。

赋予标题【主】以。
加载lib/clpars4。
调用‘命令行加回调’、
	标题 | 音量 | 图标、、1、下代码
		赋予‘参数0’【主】以‘参数1’。
	上代码、
	、视频、1、下代码
		赋予视频【主】以‘参数1’。
		加载lib/gjk4。
		如果调用‘串位置方法’、‘视频’、://小于0那么先
			加载lib/doscmd4。
			如果调用‘exist’、‘视频’否则先
				显示‘视频’不存在换行。
				结束。
			了。
		了。
	上代码、
	-h|--help、、h、下代码
		显示调用‘命令行帮助’。
		结束1。
	上代码。
调用‘命令行解析’、‘参数栈’。

加载mplayerutil.zs。
如果不安装mplayer那么结束10。
用mplayer取信息‘视频’。
如果不‘‘视频’宽度’那么
	赋予‘视频’宽度【主】以100。
如果不‘‘视频’高度’那么
	赋予‘视频’高度【主】以100。
显示‘‘视频’宽度’x‘‘视频’高度’换行。

加载lib/slave4。
调用‘初始化被动者’、下代码
	别名行以参数1。
	显示‘行’。
	如果不‘行’那么
		使主窗、销毁。
上代码。
定义【主】给我以下代码
	调用‘命令被动者’、‘参数1’。
上代码。

使主窗、侦听、
	创建为下代码
		使主窗、类名、p、p。
		如果存在图标【主】那么
			使主窗、图标、‘图标’。

		使主窗、标题、“‘标题’  F1帮助”。
		使主窗、大小、‘‘视频’宽度’、‘‘视频’高度’。
		使主窗、居中、置顶、所有桌面、无修饰。
		使主窗、事件、
			key-press-event、下代码
				显示‘参数0’--‘参数’换行。
				别名键以参数1。
				当‘键’先
					等于 space 那么先
						给我 pause。
						跳出。
					了。
					等于 Right 那么先
						给我“seek +10”。
						跳出。
					了。
					等于 Left 那么先
						给我“seek -10”。
						跳出。
					了。
					等于 period（.） 那么先
						给我“seek +1”。
						跳出。
					了。
					等于 comma（,） 那么先
						给我“seek -1”。
						跳出。
					了。
					等于 Page_Down 那么先
						给我“seek +600”。
						跳出。
					了。
					等于 Page_Up 那么先
						给我“seek -600”。
						跳出。
					了。
					等于 Up 那么先
						给我“volume +10”。
						跳出。
					了。
					等于 Down 那么先
						给我“volume -10”。
						跳出。
					了。
					大于等于1并且小于等于4
					或者等于 F2 或者等于 F3 或者等于 F4
					或者等于 equal（=） 或者等于 minus（-） 那么先
						当‘键’先
							等于minus或者等于equal那么先
								如果不存在旧宽【主】那么赋予旧宽【主】以‘‘视频’宽度’。
								如果不存在旧高【主】那么赋予旧高【主】以‘‘视频’高度’。
								赋予/*以先如果‘键’等于minus那么*否则/了(1-0.01)。
								赋予宽以算术‘旧宽’‘/*’、0。
								赋予高以算术‘旧高’‘/*’、0。
								跳出。
							了。
							调用‘头匹配方法’、‘键’、F那么先
								赋予键以调用‘子串方法’、‘键’、1。
								如果‘键’等于2那么赋予键以算术1/‘键’。
								否则赋予键以算术(‘键’+1)/‘键’。
								显示‘键’换行。
							了。
							赋予宽以算术‘‘视频’宽度’/‘键’、0。
							赋予高以算术‘‘视频’高度’/‘键’、0。
						了。
						显示‘宽’x‘高’换行。
						使主窗、大小、‘宽’、‘高’。
						赋予旧宽【主】以‘宽’。
						赋予旧高【主】以‘高’。
						跳出。
					了。
					等于 F5 或者等于 numbersign（#） 那么先
						给我“switch_audio”。
						跳出。
					了。
					等于 F11 或者等于 f 那么先
						如果存在全屏【主】那么
							删除全屏【主】。
						否则
							赋予全屏【主】以。
						使主窗、先如果不存在全屏【主】那么取消了全屏。
						跳出。
					了。
					等于 bracketright（]） 或者 等于 bracketleft（[） 那么先
						如果不存在速【主】那么赋予速【主】以1。
						赋予速【主】以算术‘速’*如果‘键’等于bracketleft那么0.5否则2。
						如果‘速’小于0.25那么赋予速【主】以0.25，跳出。
						如果‘速’大于4那么赋予速【主】以4，跳出。
						显示‘速’换行。
						给我“speed_set ‘速’”。
						跳出。
					了。
					等于 q 那么先
						使主窗、销毁。
						跳出。
					了。
					等于 F1 那么先
						加载lib/file4。
						赋予以"先调用‘得目录名’、‘参数0【主】’了help.zs"。
						显示‘’换行。
						执行‘’&。
						跳出。
					了。
				了。
			上代码、
			enter-notify-event、下代码
				加载lib/timeout4。
				调用‘移除定时器’、去框。
				调用‘新建一次定时器’、加框、0.3s、“使主窗、无修饰、0。”。
				调用‘新建一次定时器’、激活、0.4s、“使主窗、激活。”。
			上代码、
			leave-notify-event、下代码
				调用‘移除定时器’、激活。
				调用‘移除定时器’、加框。
				调用‘新建一次定时器’、去框、2s、“使主窗、无修饰、1”。
			上代码。
	上代码、
	显现为下代码
		调用‘启动被动者’、mplayer、“-wid ”先使主窗、xid了“ -slave -quiet "‘视频’" -subcp UTF-8”
			（先如果存在音量【主】那么先
				（-10约30%）
				赋予最大以60。
				赋予最小以-200。
				赋予式子以(‘最大’-‘最小’)/100*‘音量’+‘最小’。
				赋予值以算术‘式子’、0。
				显示‘值’=‘式子’换行。
				“mplayer -af volume=‘值’ -softvol -softvol-max 200”
			了了）。
		如果存在音量【主】那么
			给我“volume ‘音量’ 1”。
	上代码、
	销毁为下代码
		给我 stop。
		给我 quit。
		调用‘停止被动者’。
	上代码。

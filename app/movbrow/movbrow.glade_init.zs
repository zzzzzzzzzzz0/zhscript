加载lib/clpars4。
赋予-tmp【主】以/tmp。
调用‘命令行加回调’、
	-p、、1、下代码
		赋予【主】指定播放器以‘参数1’。
	上代码、
	-p-1、、1、下代码
		赋予【主】指定播放器参1以‘参数1’。
	上代码、
	-iwh、、2、下代码
		赋予【主】指定图片宽以‘参数1’。
		赋予【主】指定图片高以‘参数2’。
	上代码、
	-i、、1、下代码
		赋予【主】指定图片查看以‘参数1’。
	上代码、
	-tmp、、1、下代码
		赋予‘参数0’【主】以‘参数1’。
	上代码、
	、、1、下代码
		当‘参数1’先
			等于1那么赋予【主】视频目录终号以‘参数2’，跳出。
			等于2那么赋予【主】视频目录始号以‘参数2’，跳出。
		了。
	上代码。
调用‘命令行解析带序号’、‘参数栈’。

加载lib/defi。
加载lib2/msgbox。
加载lib2/iconview.zs。
加载lib/gjk4。
赋予【主】~以调用‘得环境变量方法’为HOME。
赋予【主】配置目录以‘~’/.config/movbrow_zs。
赋予【主】配置文件以‘配置目录’/setting.zs。
赋予【主】缓存目录以‘~’/.cache/movbrow_zs。
执行“mkdir -p "‘配置目录’" "‘缓存目录’" "‘缓存目录’/tmp"”。
加载lib/file4。
赋予【主】.以调用‘最终文件名’为解释‘目录名’。
赋予【主】需要同名图片以‘.’/xytmtp.png。
赋予【主】这是目录以‘.’/zsml.png（-gtk-directory）。
赋予【主】这是列表以‘.’/zsml.png。
加载lib/doscmd4。
如果调用‘exist’为‘配置文件’那么
	加载updatesetting4kw.zs，加载‘配置文件’
否则
	加载setting.zs，加载updatesetting4kw.zs、0。
赋予【主】旧应用标题以隧道标题。
定义【主】恢复标题以“隧道标题为先如果‘当前目录’不等于“”那么先调用‘子串方法’为‘当前目录’为‘根长’了“ - ”了‘旧应用标题’。”。

定义【主】扫描视频以下代码
	赋予【主】当前目录以。
	加载lib/regexpr4。
	解释‘正则顶切’为先隧道内容为movdirs了↙为([^↙]+)↙为视频目录。
	加载lib/redireconsoi4。
	如果存在【主】指定图片高那么
		隧道读图片限制为‘指定图片宽’、‘指定图片高’。
	否则
		隧道读图片限制为先隧道内容为iconw了、先隧道内容为iconh了。
	赋予【主】目录控制以如果隧道勾选为dir那么ad否则s。
	隧道可见为toolbar2为先隧道勾选为dir了。
	
	隧道图标夹为清空为‘图标夹’。如果存在【主】刷新那么删除【主】刷新。
	赋予号以0。
	循环先
		赋予号以算术‘号’+1。
		如果不存在视频目录之‘号’【顶】那么跳出。
		如果存在视频目录始号【主】并且‘号’小于‘视频目录始号’那么再来。
		如果存在视频目录终号【主】并且‘号’大于‘视频目录终号’那么跳出。
		如果调用‘头匹配方法’为‘视频目录之‘号’’为-那么先
			赋予视频目录之‘号’以调用‘子串方法’为‘视频目录之‘号’’为1。
			显示‘视频目录之‘号’’跳过↙。
			再来。
		了。
		隧道标题为“‘旧应用标题’（扫描 ‘视频目录之‘号’’）”。
		扫描目录‘视频目录之‘号’’/。
	了。
	恢复标题。
上代码。
加载lib2/is_video.zs。
定义【主】扫描目录以下代码
	赋予搜以调用‘dir.begin’为‘参数1’、、‘目录控制’o3。
	循环先
		赋予文件以调用‘dir.next’为‘搜’。
		如果不‘文件’那么跳出。
		赋予标题以调用‘省略名’为‘文件’为x、/-、_。
		赋予路径以‘参数1’‘文件’。
		隧道不堵塞。
		当先
			调用‘尾匹配方法’为‘文件’为/那么赋予缩略图以‘这是目录’，跳出。
			调用‘尾匹配方法’为‘文件’为.m3u那么赋予缩略图以‘这是列表’，跳出。
			那么先
				如果不是视频‘路径’那么再来。
				赋予目录以解释‘目录名’为‘路径’。
				赋予名以调用‘得文件主名’、‘路径’。
				赋予名以调用‘正则格’、‘名’、([^.]*)。
				赋予名以调用‘加反斜杠’、‘名’、R。
				赋予搜图以调用‘dir.begin’、‘目录’、^‘名’\..*(PNG|JPG|JPEG)$、l。
				赋予缩略图以调用‘dir.next’、‘搜图’。
				调用‘dir.end’、‘搜图’。
				如果‘缩略图’那么赋予缩略图以‘目录’‘缩略图’否则先
					赋予缩略图以得缓存缩略图名。
					如果调用‘exist’为‘缩略图’等于0那么赋予缩略图以‘需要同名图片’。
				了。
				赋予zs以‘目录’‘名’.zs。
				如果调用‘exist’、‘zs’那么加载‘zs’。
			了。
		了。
		如果存在“【主】结束”那么显示放弃↙，结束。
		如果存在【主】刷新那么删除【主】刷新，结束。
		图标夹增加‘图标夹’为‘路径’、‘标题’、‘缩略图’、、。
	了。
	调用‘dir.end’为‘搜’。
上代码。

定义【主】刷新当前目录以下代码
	隧道图标夹为清空为‘图标夹’。
	扫描目录‘当前目录’/。
	恢复标题。
上代码。

定义【主】得缓存缩略图名以下代码
	如果‘当前目录’等于“”那么
		赋予缩略图以‘标题’。
	否则先
		赋予缩略图以如果存在【上】路径那么‘【上】路径’否则‘视频’。
		赋予缩略图以调用‘子串方法’为‘缩略图’为‘根长’。
		赋予缩略图以调用‘省略名’为‘缩略图’为x、/-、_。
	了。
	‘缓存目录’/‘缩略图’.png。
上代码。

赋予【主】图标夹以隧道图标夹为iconview1、代码为下代码
	如果存在参数3那么先
		如果‘参数3’等于活性那么播放当前视频，退出。
		如果等于菜单那么隧道提示为‘参数’，退出。
	了。
	更新状态栏‘参数1’。
上代码、菜单为menu1、外菜单为menu2、图片缓存为隧道图片缓存。

赋予【主】管道以‘配置目录’/mplayer.fifo。
定义【主】给我以下代码
	如果调用‘fileinfo’为‘管道’为%m否则更新状态栏“不能 ‘参数’ ，未打开 或 非”先隧道内容为embedplayer了“、”先隧道内容为player1了，退出。
	执行显示“echo ‘参数’ > ‘管道’ &”↙。
上代码。

定义【主】测试安装以下代码
	赋予“ ”以调用‘串位置方法’为‘参数1’为“ ”。
	如果‘ ’大于等于0那么赋予命令名以调用‘子串方法’为‘参数1’为0、‘ ’。
	否则赋予命令名以‘参数1’。
	赋予包名以如果存在参数2那么‘参数2’否则‘命令名’。
	循环先
		赋予名以调用‘最终文件名’为‘命令名’。显示‘命令名’---‘名’;换行。
		如果不‘名’那么先
			隧道标题为“‘旧应用标题’（安装 ‘包名’ ，失去反应请稍候）”。
			隧道不堵塞。
			如果执行显示“apturl-gtk apt:‘包名’”↙等于0那么恢复标题，跳出。
			如果执行显示“gksudo "apt-get -y install ‘包名’"”↙等于0那么恢复标题，跳出。
			如果隧道提示为“请安装所需的‘包名’，完毕后点确定”、“找不到且无法自动安装‘命令名’”、‘信息’、‘确定取消按钮’不等于‘确定’那么
				0，退出。
		了否则跳出。
	了。
	1。
上代码。

定义【主】播放器选择以下代码
	别名英以参数1。
	赋予汉以当‘英’先
		等于explayer那么外，跳出。
		等于embedplayer那么内，跳出。
		等于player1那么简，跳出。
	了。
	如果存在【主】指定播放器那么先
		赋予回以如果‘指定播放器’等于‘汉’那么1否则0。
	了否则
		赋予回以隧道勾选为‘英’。
	如果‘回’那么显示‘英’‘汉’↙。
	‘回’
上代码。

定义【主】播放当前视频以下代码
	如果图标夹获取‘图标夹’为下代码
		赋予【上】视频以‘参数1’。
	上代码那么先
		如果调用‘尾匹配方法’为‘视频’为/那么先
			赋予路径以调用‘子串方法’为‘视频’为0、-1。
			如果‘当前目录’等于“”那么先
				赋予目录以解释‘目录名’为‘路径’。
				加载lib/stdc。
				赋予【主】根长以调用‘串长函数’为‘目录’。
			了。
			赋予【主】当前目录以‘路径’。
			刷新当前目录。
			退出。
		了。
		
		如果播放器选择explayer那么先
			赋予命令以迭代解释下代码
				别名项以参数1。
				如果存在【主】指定播放器参1那么先
					赋予勾选以如果‘项’等于‘指定播放器参1’那么1否则0。
				了否则
					赋予勾选以隧道勾选为‘项’。
				如果‘勾选’那么先
					赋予返回以隧道提示文字为‘项’。
					如果不‘返回’那么
						赋予返回以隧道内容为‘项’。
					如果测试安装‘返回’为‘项’否则跳出。
					‘返回’。
					跳出。
				了。
			上代码、mplayer、mplayer-gui、“mplayer #caca”、ffmpeg、gnome-mplayer、totem、vlc、xine-ui。
			如果‘命令’那么先
				调用‘正则攫’、‘命令’、([^↙]+)↙(.*)、命令。
				如果‘命令数目2’大于0那么
					赋予命令以‘命令之1’。
				如果调用‘尾匹配方法’为‘视频’为.m3u并且‘命令数目2’大于等于2那么
					赋予命令以“‘命令’ ‘命令之2’”。
				执行显示“‘命令’ "‘视频’"&”↙。
				如果隧道勾选为minionplay那么
					隧道窗口最小化。
			了。
			退出。
		了。
		
		如果测试安装mplayer否则结束。
		隧道点击为stop。
		
		如果播放器选择embedplayer那么先
			隧道内容为notebook1为2。
			赋予wid以隧道xid为drawingarea1。
			如果‘wid’那么先
				执行“mkfifo ‘管道’”。
				赋予命令以“mplayer -wid ‘wid’ -slave -quiet -input file=‘管道’ ”
					先如果调用‘尾匹配方法’为‘视频’为.m3u那么“-playlist ”了“"‘视频’" &”。
				执行显示‘命令’↙。
			了。
			退出。
		了。
		如果播放器选择player1那么先
			赋予命令以‘无边行头’先
				如果存在【主】指定播放器参1那么‘指定播放器参1’否则隧道内容为p1_args
			了“ ”"‘视频’"。
			显示‘命令’↙。
			赋予【主】简播进程号以同时执行‘命令’。
			如果隧道勾选为minionplay那么
				隧道窗口最小化。
			退出。
		了。
	了。
上代码。

定义【主】杀死简播以下代码
	如果存在【主】简播进程号那么先
		执行显示“kill ‘简播进程号’”（↙）。
		删除【主】简播进程号。
	了。
上代码。


隧道侦听为点击为play、m_play为‘播放当前视频’。
隧道侦听为点击为pause为下代码
	给我‘动作’。
上代码。
隧道侦听为点击为stop为下代码
	如果给我‘动作’不等于0那么退出。
	给我exit。
	如果播放器选择player1那么
		杀死简播。
	执行显示“rm ‘管道’”↙。
上代码。

隧道侦听为点击为shot、shot2、m_shot、m_shot2为下代码
	图标夹获取‘图标夹’为下代码
		赋予视频以‘参数1’。
		如果调用‘尾匹配方法’为‘视频’为/那么更新状态栏文件夹不能‘【上】参数1’，退出。
		赋予标题以‘参数2’。
		如果‘动作’等于shot或者等于m_shot那么
			赋予缩略图以得缓存缩略图名。
		否则
			赋予缩略图以先解释‘目录名’为‘视频’了先解释‘文件主名’为‘视频’了.png。
		调用‘随机数方法’为0。
		如果隧道勾选为s_mplayer那么先
			获取info‘视频’。
			赋予位置以调用‘随机数方法’为算术‘‘视频’长度’*100、1。
			赋予位置以算术‘位置’/100。
			赋予命令以“cd "‘缓存目录’/tmp" && mplayer -ss ‘位置’ -noframedrop -nosound -vo png -frames 1 "‘视频’" && mv *.png "‘缩略图’"”。
		了否则先
			赋予位置以调用‘随机数方法’为5、1。
			赋予命令以“ffmpeg -i "‘视频’" -y -f image2 -ss ‘位置’ -t 0.5 "‘缩略图’"”。
			如果测试安装‘命令’否则结束。
		了。
		执行显示‘命令’↙。
		图标夹更新‘图标夹’为‘视频’、‘标题’、‘缩略图’、为‘参数5’。
	上代码。
上代码。

隧道侦听为点击为show、m_show为下代码
	图标夹获取‘图标夹’为下代码
		别名图片以参数3。
		如果存在【主】指定图片查看那么
			赋予命令以‘指定图片查看’。
		了否则
			赋予命令以隧道内容为show_cmd。
		执行显示“‘命令’ "‘图片’"&”↙。
	上代码。
上代码。

隧道侦听为点击为del、del2、m_del、m_del2为下代码
	赋予动作以‘动作’。
	图标夹移除‘图标夹’为下代码
		别名视频以参数1。
		别名图片以参数3。
		赋予目录以调用‘得目录名’、‘视频’。
		赋予名以调用‘得文件主名’、‘视频’。
		赋予zs以‘目录’‘名’.zs。
		如果调用‘exist’、‘zs’否则赋予zs以。
		如果‘动作’等于del或者等于m_del那么先
			赋予命令以“rm -rf "‘视频’"”。
			如果‘图片’不等于‘需要同名图片’那么
				赋予命令以‘命令’↙“rm "‘图片’"”。
			如果‘zs’那么
				赋予命令以‘命令’↙“rm "‘zs’"”。
		了否则先
			赋予命令以“mv "‘视频’" ‘-tmp’”。
			如果‘图片’不等于‘需要同名图片’那么
				赋予命令以‘命令’↙“mv "‘图片’" ‘-tmp’”。
			如果‘zs’那么
				赋予命令以‘命令’↙“mv "‘zs’" ‘-tmp’”。
		了。
		赋予命令以‘命令’↙“rmdir -p "‘目录’" #如果目录为空将删除”。
		如果隧道提示为‘命令’、‘【上】参数1’、‘询问’、‘是否按钮’等于‘是’那么执行显示‘命令’↙，1，退出。
		0。
	上代码。
上代码。

定义获取info【主】以下代码
	别名视频以参数1。
	如果存在【主】‘视频’大小那么退出。
	显示‘参数0’‘参数’↙。
	
	定义得信息以“调用‘fileinfo’为‘视频’为%‘参数1’”。
	定义得时间以“调用‘串格给时间方法’为“%x %X”为得信息t‘参数1’”。
	赋予‘视频’大小【主】以大小:→先得信息s了“ 字节”。
	赋予‘视频’访问时间【主】以访问:→得时间a。
	赋予‘视频’建立时间【主】以建立:→得时间c。
	赋予‘视频’修改时间【主】以修改:→得时间m。
	
	如果测试安装mplayer否则退出。
	赋予信息以调用‘重定向输出’、“mplayer -identify "‘视频’" -nosound -vc dummy -vo null”。
	赋予‘视频’长度【主】以调用‘regexpr’、‘信息’、ID_LENGTH=([0-9.]+)。
	赋予‘视频’宽度【主】以调用‘regexpr’、‘信息’、ID_VIDEO_WIDTH=([0-9]+)。
	赋予‘视频’高度【主】以调用‘regexpr’、‘信息’、ID_VIDEO_HEIGHT=([0-9]+)。
上代码。

隧道侦听为点击为info、m_info为下代码
	图标夹获取‘图标夹’为下代码
		赋予视频以‘参数1’。
		赋予标题以‘参数2’。
		获取info‘视频’。
		隧道提示为
			先
			‘标题’
			先
				如果存在‘视频’演员表【主】那么
					‘‘视频’演员表’
				否则
					↙
			了
			↙
			路径:→‘视频’↙↙
			‘‘视频’大小’空格/空格‘‘视频’长度’空格‘‘视频’宽度’x‘‘视频’高度’↙↙
			‘‘视频’访问时间’↙
			‘‘视频’修改时间’↙
			‘‘视频’建立时间’
			了
			为‘参数1【上】’。
	上代码。
上代码。

隧道侦听为点击为refresh、m_refresh、root、up为下代码
	循环先
		如果‘动作’不等于root那么先
			如果‘当前目录’不等于“”那么先
				如果‘动作’等于up那么先
					赋予应选中的名字以‘当前目录’/。
					赋予【主】当前目录以调用‘移除..’为‘当前目录’/..。
					加载lib/stdc。
					如果调用‘串长函数’为‘当前目录’小于等于‘根长’那么跳出。
				了。
				如果隧道勾选为dir那么先
					刷新当前目录。
					如果存在应选中的名字那么图标夹选择‘图标夹’为‘应选中的名字’、滚至是实。
					退出。
				了。
			了。
		了。
		跳出。
	了。
	赋予“【主】刷新”以。
	扫描视频。
	如果存在应选中的名字那么图标夹选择‘图标夹’为‘应选中的名字’、滚至是实。
上代码。

隧道侦听为点击为add_dir为下代码
	赋予文件以隧道文件选择为文件夹、‘参数1’。
	如果‘文件’那么
		隧道插入为movdirs为↙‘文件’。
上代码。

赋予【主】无边行头以"先解释“
		赋予1以‘.’/movbrow。
		如果调用‘exist’为‘1’那么‘1’，退出。
		‘.’/../../new_o/gtkmmsh。
	”了“" "‘.’/player1.glade" -f‘管道’ ”。
隧道侦听为点击为p1_help为下代码
	同时执行‘无边行头’先隧道内容为p1_args了。
上代码。

隧道定时器为“扫描视频”为200、下代码
	隧道定时器为‘动作’为移除。
	扫描视频。
上代码。

定义【主】更新状态栏以下代码
	隧道内容为statusbar1为‘参数1’。
	隧道定时器为清除状态栏为5000为下代码
		隧道定时器为‘动作’为移除。
		隧道内容为statusbar1为。
	上代码。
上代码。

如果隧道勾选为fullscreen那么隧道窗口最大化。
